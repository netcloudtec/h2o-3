apply plugin: 'java'

description = 'H2O driver for Hadoop ' + hadoopVersion

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava { 
  options.debug = true 
}

sourceSets {
  main {
    java {
      if (project.hasProperty('notYarn')) {
        srcDirs  '../h2o-mapreduce-generic/src/main/java'
      } else {
        srcDirs '../h2o-mapreduce-generic/src/main/java',
                '../h2o-yarn-generic/src/main/java'
      }
    }
    resources {
      if (project.hasProperty('notYarn')) {
        srcDirs  '../h2o-mapreduce-generic/src/main/resources'
      } else {
        srcDirs '../h2o-mapreduce-generic/src/main/resources',
                '../h2o-yarn-generic/src/main/resources'
      }
    }
  }
}

dependencies {
  compile "org.eclipse.jetty.aggregate:jetty-all-server:8.1.17.v20150415"
  if (project.hasProperty('notYarn')) {
    compile('org.apache.hadoop:hadoop-core:' + hadoopMavenArtifactVersion)
  }
  else {
    compile('org.apache.hadoop:hadoop-client:' + hadoopMavenArtifactVersion)
  }
  compile project(':h2o-app')
  compile project(":h2o-web")
  compile project(":h2o-avro-parser")
}

